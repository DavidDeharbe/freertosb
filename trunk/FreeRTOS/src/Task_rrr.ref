/* Task_rrr
 * Author: stepgalvao
 * Creation date: Thu Sep 24 2009
 */
REFINEMENT
   Task_rrr
REFINES
   Task_rr

SEES
   FreeRTOSConfig ,
   Types

ABSTRACT_VARIABLES
   active ,
   tasks ,
   blocked ,
   running ,
   ready ,
   suspended ,
   idle ,
   t_priority ,
   t_scheduler ,
   
   /*Refinement variable*/
   t_unblocked

  
   
INVARIANT
    t_unblocked : TASK  +-> TICK &
    dom(t_unblocked) <: TASK &
    dom(t_unblocked) <: tasks &
    blocked <: dom(t_unblocked) 
    
INITIALISATION
   active := FALSE ||
   tasks := {} ||
   running :: TASK ||
   idle :: TASK ||
   t_priority := {} ||
   t_scheduler := {} ||
   blocked, ready, suspended := {},{},{}||
   t_unblocked := {}

OPERATIONS
 	result <-- t_create (priority) =
    BEGIN	
        ANY
            task
        WHERE
            task : TASK &
            task /: tasks
        THEN     	    
            tasks := {task} \/ tasks ||
            t_priority:= t_priority \/{task|->priority}||
            ready:=ready \/ {task}||
            t_scheduler:=t_scheduler\/{task|->0} ||
            t_unblocked := t_unblocked \/{task|->0}||
            result := task
        END                  
    END
    ;
    
    t_delete (atask)=
    PRE
        atask : TASK &
        atask : tasks &
        atask /= idle
    THEN
        tasks := tasks - { atask } || 	
        t_priority:= { atask } <<| t_priority ||
		t_scheduler:= { atask } <<| t_scheduler ||
		t_unblocked:= { atask } <<| t_unblocked ||

        IF atask = running	THEN
            ANY
                task
            WHERE 
                task : TASK & task : schedule_p (ready, t_priority)
            THEN
                running := task ||
                ready := ready - { task }
            END		
        ELSIF atask : ready  THEN
            ready := ready - { atask }
        ELSIF atask : blocked  THEN
            blocked := blocked - { atask }
        ELSIF atask : suspended THEN
            suspended := suspended - { atask }
        END
    END
    ;
    
    t_endScheduler =
    BEGIN
        active := FALSE ||
        tasks := {} ||
        t_priority,t_scheduler,t_unblocked:= {},{},{}||
        blocked , ready , suspended := {},{},{}
    END
    
END
